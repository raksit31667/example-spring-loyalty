plugins {
    id 'idea'
    id 'java'
    id 'org.owasp.dependencycheck' version '7.1.0.1'
    id 'org.sonarqube' version '3.3'
    id 'com.github.ben-manes.versions' version '0.41.0'
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'org.owasp.dependencycheck'
    apply plugin: 'org.sonarqube'
    apply plugin: 'com.github.ben-manes.versions'

    group = "com.raksit.example"
    version = "0.0.1-SNAPSHOT"
    sourceCompatibility = 17

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
}

subprojects {
    task createFlywayMigrationScript {
        doLast {
            def fileDescription = "your_description"
            if (project.hasProperty("scriptName")) {
                fileDescription = project.getProperty("scriptName").toLowerCase().replace(" ", "_")
            }
            def fileName= "V${new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT"))}__${fileDescription}"
            new File("${project.projectDir}/src/main/resources/db/migration/${fileName}.sql").createNewFile()
        }
    }

    dependencyCheck {
        failBuildOnCVSS = 6
        suppressionFile = 'config/dependencycheck/suppressions.xml'
    }

    tasks.named("dependencyUpdates").configure {
        rejectVersionIf {
            !isStable(it.candidate.version) && isStable(it.currentVersion)
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "raksit31667_example-spring-loyalty"
        property "sonar.projectName", "example-spring-loyalty"
        property "sonar.organization", "raksit31667"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", ${env.SONARQUBE_LOGIN}
        property "sonar.projectBaseDir", "api-service"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/test/jacocoTestReport.xml"
        property 'sonar.java.source', "11"
        property "sonar.coverage.exclusions", "**/dto/**, **/entity/**, **/exception/**, **/config/**, " +
                "**/security/**, **/cache/**, **/specification/**"
    }
}

static def isStable(version) {
    def stableKeyword = ["RELEASE", "FINAL", "GA"].any {
        version.toUpperCase().contains(it)
    }
    def otherReleasePattern = version ==~ '^[0-9,.v-]+(-r)?$'

    return stableKeyword || otherReleasePattern
}
