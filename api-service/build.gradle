plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
}

repositories {
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2' }
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

group = 'com.raksit.example'
version = '0.0.1'
sourceCompatibility = 17

ext {
    set('springBootVersion', '2.5.0')
    set('springCloudVersion', '2020.0.3')
    set('springCloudAwsVersion', '2.3.2')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:${springCloudAwsVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.projectlombok:lombok:1.18.22'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.7.3'
    implementation 'net.rakugakibox.spring.boot:logback-access-spring-boot-starter:2.7.1'
    implementation 'io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1'
    implementation 'io.awspring.cloud:spring-cloud-starter-aws'
    implementation 'com.amazonaws:aws-java-sdk-sts:1.12.105'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kinesis:2.2.0'
    implementation 'io.split.client:java-client:3.3.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'io.rest-assured:rest-assured:3.1.0'
    testImplementation 'io.zonky.test:embedded-database-spring-test:2.1.0'
    testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:7.0.0'
    testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
}

processResources {
    filesMatching('application.yaml') {
        expand(project.properties)
    }
}

testSets {
    integrationTest {
        dirName = 'integration-test'
    }
}

test {
    testLogging.showStandardStreams = false
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    finalizedBy jacocoTestReport
}

integrationTest {
    testLogging.showStandardStreams = false
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
}

bootRun {
    if (System.properties.containsKey('spring.profiles.active')) {
        systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
    } else {
        systemProperty 'spring.profiles.active', 'local'
    }
}

checkstyle {
    toolVersion = "8.33"
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    maxErrors = 0
    maxWarnings = 0
    reportsDir = file("$project.buildDir/reports/checkstyle")
}

pmd {
    toolVersion = "6.21.0"
    ignoreFailures = false
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = ["java-basic", "java-braces"]
}

jacoco {
    toolVersion = '0.8.7'
    reportsDir = file("$buildDir/reports/jacoco")
}

def jacocoExcludedClasses = [
        "com/raksit/example/loyalty/LoyaltyApplication*",
        "com/raksit/example/loyalty/user/controller/*",
        "com/raksit/example/loyalty/user/dto/*",
        "com/raksit/example/loyalty/config/*",
        "com/raksit/example/loyalty/error/ErrorResponse*",
        "com/raksit/example/loyalty/feature/*",
        "com/raksit/example/loyalty/activity/event/*",
        "com/raksit/example/loyalty/transaction/entity/*",
        "com/raksit/example/loyalty/metric/*",
        "com/raksit/example/loyalty/tracing/LogCorrelationConfiguration*"
]

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
    classDirectories.setFrom(classDirectories.files.collect {
        fileTree(dir: it, exclude: jacocoExcludedClasses)
    })
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
        html.destination file("$buildDir/reports/jacoco")
    }
    afterEvaluate {
        classDirectories.setFrom(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoExcludedClasses)
        })
    }
}

task zipReports(type: Zip) {
    archiveFileName = "${rootProject.name}-${project.name}-reports.zip"
    destinationDirectory = file("$buildDir/check-reports")

    from("$buildDir/reports")
}

zipReports {
    dependsOn check
}

check.dependsOn(jacocoTestCoverageVerification, integrationTest)
test.shouldRunAfter(pmdMain)
integrationTest.shouldRunAfter(jacocoTestCoverageVerification)
jacocoTestReport.dependsOn(test)
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

